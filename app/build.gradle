plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'io.fabric'
}

def lifecycle_version = '2.2.0'
def dagger_version = '2.27'

android {
    signingConfigs {
        default_test {
            def testPass = file(project.properties["store_pass"] as String).text
            storeFile file(project.properties["store_file"] as String)
            storePassword = testPass
            keyPassword = testPass
            keyAlias project.properties['lps_alias'] as String
        }
    }

    sourceSets {
        test {
            resources.srcDirs += ['sampledata']
        }
        androidTest {
            resources.srcDirs += ['sampledata']
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    def HOST_LOCAL = "\"192.168.2.244\""
    def HOST_REMOTE = "\"194.87.111.204\""

    compileSdkVersion 29
    defaultConfig {
        applicationId "ru.aleshi.letsplaycities"
        multiDexEnabled true
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 2840
        versionName "2.8.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "server"
    productFlavors {
        local {
            buildConfigField("String", "HOST", HOST_LOCAL)
            dimension "server"
        }
        remote {
            buildConfigField("String", "HOST", HOST_REMOTE)
            dimension "server"
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.3.4"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navVersion"
    implementation "androidx.navigation:navigation-runtime-ktx:$rootProject.navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'
    implementation 'com.google.android.gms:play-services-ads-lite:19.1.0'
    implementation 'com.google.android.gms:play-services-games:19.0.0'
    implementation 'com.google.firebase:firebase-core:17.4.3'
    implementation 'com.google.firebase:firebase-messaging:20.2.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.mikhaellopez:circularimageview:4.0.1'
    implementation 'com.android.billingclient:billing:2.2.1'

    implementation 'com.vk:androidsdk:2.2.1'
    implementation 'com.facebook.android:facebook-login:5.15.1'
    implementation 'ru.ok:odnoklassniki-android-sdk:2.1.8'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'ru.aleshi.lps:lps-client:1.2.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    testImplementation 'junit:junit:4.13'
    testImplementation "org.mockito:mockito-core:3.0.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    androidTestImplementation 'androidx.test:runner:1.2.0'

    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:${dagger_version}"
    kapt 'javax.xml.bind:jaxb-api:2.3.1'
    kapt 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    kapt 'com.sun.xml.bind:jaxb-impl:2.3.2'
    annotationProcessor 'javax.xml.bind:jaxb-api:2.3.1'
    annotationProcessor 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    annotationProcessor 'com.sun.xml.bind:jaxb-impl:2.3.2'
    compileOnly 'com.github.pengrad:jdk9-deps:1.0'
}
